plugins {
    id 'java'
    id 'idea'
    id 'maven-publish'
}

repositories {
    mavenLocal()
    mavenCentral()
}

group = 'diktamen'
version = '1.6.1.0'
sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava {
    options.compilerArgs += ['-encoding', 'UTF-8']
}

javadoc {
    options.encoding = 'UTF-8'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/diktamen/Java-WebSocket")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        maven(MavenPublication) {
            groupId = 'diktamen'
            artifactId = 'Java-WebSocket'
            version = project.version

            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'Java-WebSocket'
                description = 'A barebones WebSocket client and server implementation written 100% in Java'
                url = 'https://github.com/diktamen/Java-WebSocket'

                scm {
                    url = 'https://github.com/diktamen/Java-WebSocket'
                    connection = 'https://github.com/diktamen/Java-WebSocket'
                    developerConnection = 'https://github.com/diktamen/Java-WebSocket'
                }

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/diktamen/Java-WebSocket/blob/master/LICENSE'
                    }
                }

                developers {
                    developer {
                        id = 'TooTallNate'
                        name = 'Nathan Rajlich'
                        email = 'nathan@tootallnate.net'
                    }
                    developer {
                        id = 'marci4'
                        name = 'Marcel Prestel'
                        email = 'admin@marci4.de'
                    }
                    developer {
                        id = 'lwahonen'
                        name = 'LW Ahonen'
                        email = 'will@vaimea.ca'
                    }
                }
            }
        }
    }
}

dependencies {
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '2.0.15'
    testImplementation group: 'org.slf4j', name: 'slf4j-simple', version: '2.0.15'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.11.4'
}

test {
    useJUnitPlatform()  // This is required for JUnit 5
}
